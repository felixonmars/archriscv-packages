# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=libffi
pkgname=libffi-8
pkgver=3.4.2
pkgrel=1
pkgdesc='Portable foreign function interface library'
arch=('riscv64')
url='https://sourceware.org/libffi/'
license=('MIT')
depends=('glibc')
checkdepends=('dejagnu')
source=(https://github.com/libffi/libffi/releases/download/v${pkgver}/libffi-${pkgver}.tar.gz)
sha256sums=('540fb721619a6aba3bdeef7d940d8e9e0e6d2c193595bc243241b77ff9e93620')
b2sums=('a8137bc895b819f949fd7705e405be627219c6d1fdef280253330f7407d4a548bb057d7bb0e9225d1767d42f9bf5f0ab3c455db1c3470d7cc876bb7b7d55d308')

prepare() {
  cd $_pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $_pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-docs \
    --enable-pax_emutramp
  make
}

check() {
  make -C $_pkgname-$pkgver check || true
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rvf "$pkgdir"/usr/{include,share/man,lib/{pkgconfig,*.so}}
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname
}

# vim: ts=2 sw=2 et:
