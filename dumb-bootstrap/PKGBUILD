# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# Contributor: Bjørn Lindeijer <bjorn@lindeijer.nl>

_pkgname=dumb
pkgname=$_pkgname-bootstrap
pkgver=2.0.3
pkgrel=3
pkgdesc='IT, XM, S3M and MOD player library'
arch=(x86_64 riscv64)
license=(custom:dumb GPL)
url='https://dumb.sf.net/'
makedepends=(cmake ninja)
provides=($_pkgname)
source=($pkgname-$pkgver.tar.gz::"https://github.com/kode54/dumb/archive/$pkgver.tar.gz")
sha256sums=('99bfac926aeb8d476562303312d9f47fd05b43803050cd889b44da34a9b2a4f9')

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$_pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_ALLEGRO4=OFF \
    -DBUILD_EXAMPLES=OFF \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 "$_pkgname-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
