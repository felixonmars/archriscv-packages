# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Daniele Basso <d dot bass05 at proton dot me>
# Contributor: T. Jameson Little <t.jameson.little at gmail dot com>
# Contributor: Usagi Ito <usagi@WonderRabbitProject.net>
# Contributor: siasia <http://pastebin.com/qsBEmNCw>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: The one with the braid <info@braid.business>

_pkgname=dart
pkgname=dart-bootstrap
pkgver=3.4.4
pkgrel=1
pkgdesc='The dart programming language SDK'
arch=('riscv64')
url='https://dart.dev/'
depends=('glibc')
license=('BSD')
provides=('dart')
makedepends=(
  'git'
  'gn'
  'ninja'
  'python'
  'python-httplib2'
  'python-six'
)
source=(
  "git+https://github.com/dart-lang/sdk.git#tag=$pkgver"
  "git+https://chromium.googlesource.com/chromium/tools/depot_tools.git"
  "DEPS.patch"
  "package_config.json"
  "dart-riscv-no-cross.patch"
)
sha256sums=('aba863b230001773aa8cbbedb2824de154f778377c4048dbcc12460fdcd969f5'
            'SKIP'
            'db6576a70c6719e26795b9824546058b79fefa64158c1002d36546d826084403'
            'c91bb6c87b1d8af3417c12b4eb5283475f3cf1ddb4910784fb542f29dbc0f21f'
            '9b69b12208faa1a4b98be5e5e73385526e2571f0f3527aadd0496eadb4b7aab6')

prepare() {
cat >.gclient <<EOF
solutions = [
  {
    "name": "sdk",
    "url": "file://${srcdir}/sdk",
    "deps_file": "DEPS",
    "managed": False,
    "custom_deps": {},
    "custom_vars": {},
  },
]
EOF

  export PATH+=":$PWD/depot_tools" DEPOT_TOOLS_UPDATE=0

  cd sdk

  patch -Np 1 --input=$srcdir/DEPS.patch
  patch -Np 1 --input=$srcdir/dart-riscv-no-cross.patch

  python ../depot_tools/gclient.py sync -D \
      --nohooks \
      --no-history \
      --shallow \
      -r ${srcdir}/sdk@${_commit}

  cp ../package_config.json .dart_tool/
  python tools/generate_sdk_version_file.py

  #ln -s /usr/bin/gn buildtools/gn
  #mkdir -p buildtools/ninja
  #ln -s /usr/bin/ninja buildtools/ninja/ninja
  sed -i 's|prefix = rebased_clang_dir|prefix= ""|g' build/toolchain/linux/BUILD.gn # use system clang
  sed -i 's|}/|}|g' build/toolchain/linux/BUILD.gn # use system clang
  sed -i 's|rebase|#|g' build/toolchain/linux/BUILD.gn
}

build() {
  cd sdk

  # gn args --list out

  /usr/bin/gn gen -qv out --args='
                        target_cpu = "riscv64"
                        is_debug = false
                        is_release = true
                        is_clang = false
                        dart_platform_sdk = false
                        verify_sdk_hash = false'
  ninja create_sdk -v -C out
}

package() {
  # cd to directory
  cd sdk/out/

  # Create directories
  install -d "$pkgdir"{"/opt/$_pkgname-sdk",/usr/{bin,"share/doc/$_pkgname"}}

  # Package the files
  cp -a "$_pkgname-sdk/"* "$pkgdir/opt/$_pkgname-sdk/"

  # Set up symbolic links for the executables
  for f in dart dartaotruntime; do
    ln -s "/opt/$_pkgname-sdk/bin/$f" "$pkgdir/usr/bin/$f"
  done

  # Package documentation
  install -Dm644 "$pkgdir/opt/$_pkgname-sdk/README" -t "$pkgdir/usr/share/doc/$_pkgname"

  # BSD License
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
