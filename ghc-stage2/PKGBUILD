# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

shopt -s extglob

_pkgbase=ghc
pkgname=ghc-stage2
pkgver=9.0.1
pkgrel=1
pkgdesc='The Glasgow Haskell Compiler (stage 2)'
arch=('riscv64')
url='https://www.haskell.org/ghc/'
license=('custom')
makedepends=(
    ghc-bootstrap-riscv64
    ghc-bootstrap-helpers

    'clang' 'lld' 'python3'
    'perl' 'libxslt' 'docbook-xsl' 'python-sphinx'
    'texlive-bin' 'texlive-latexextra' 'ttf-dejavu' 'time'
)
depends=('gcc' 'gmp' 'libffi' 'llvm' 'perl' 'numactl')
provides=("ghc-static=$pkgver"
          "ghc=$pkgver"
          "ghc-libs=$pkgver"
          'haskell-haddock=2.24.0'
          'haskell-hp2ps=0.1'
          'haskell-hpc-bin=0.68'
          'haskell-hsc2hs=0.68.7'
          "haskell-ghc=$pkgver"
          "haskell-ghci=$pkgver"
          'haskell-array=0.5.4.0'
          'haskell-base=4.15.0.0'
          'haskell-binary=0.8.8.0'
          'haskell-bytestring=0.10.12.1'
          'haskell-cabal=3.4.0.0'
          'haskell-containers=0.6.4.1'
          'haskell-deepseq=1.4.5.0'
          'haskell-directory=1.3.6.1'
          'haskell-exceptions=0.10.4'
          'haskell-filepath=1.4.2.1'
          'haskell-ghc-bignum=1.0'
          'haskell-ghc-boot=9.0.1'
          'haskell-ghc-boot-th=9.0.1'
          'haskell-ghc-compact=0.1.0.0'
          'haskell-ghc-heap=9.0.1'
          'haskell-ghc-prim=0.7.0'
          'haskell-haskeline=0.8.1.0'
          'haskell-hpc=0.6.1.0'
          'haskell-integer-gmp=1.1'
          'haskell-libiserv=9.0.1'
          'haskell-mtl=2.2.2'
          'haskell-parsec=3.1.14.0'
          'haskell-pretty=1.1.3.6'
          'haskell-process=1.6.11.0'
          'haskell-stm=2.5.0.0'
          'haskell-template-haskell=2.17.0.0'
          'haskell-terminfo=0.4.1.4'
          'haskell-text=1.2.4.1'
          'haskell-time=1.9.3'
          'haskell-transformers=0.5.6.2'
          'haskell-unix=2.7.2.2'
          'haskell-xhtml=3000.2.2.1'
          'haskell-ghc-pkg=6.9')
replaces=('haskell-haddock'
          'haskell-hp2ps'
          'haskell-hpc-bin'
          'haskell-hsc2hs'
          'haskell-ghc'
          'haskell-array'
          'haskell-base'
          'haskell-binary'
          'haskell-bytestring'
          'haskell-cabal'
          'haskell-containers'
          'haskell-deepseq'
          'haskell-directory'
          'haskell-exceptions'
          'haskell-filepath'
          'haskell-ghc-bignum'
          'haskell-ghc-boot'
          'haskell-ghc-boot-th'
          'haskell-ghc-compact'
          'haskell-ghc-heap'
          'haskell-ghc-prim'
          'haskell-haskeline'
          'haskell-hpc'
          'haskell-integer-gmp'
          'haskell-libiserv'
          'haskell-mtl'
          'haskell-parsec'
          'haskell-pretty'
          'haskell-process'
          'haskell-stm'
          'haskell-template-haskell'
          'haskell-terminfo'
          'haskell-text'
          'haskell-time'
          'haskell-transformers'
          'haskell-unix'
          'haskell-xhtml'
          'haskell-ghc-pkg')
conflicts=('ghc-static' 'ghc' 'ghc-libs' 'haskell-ghci')

source=("https://downloads.haskell.org/~ghc/$pkgver/$_pkgbase-${pkgver}-src.tar.xz"
        ghc-rebuild-doc-index.hook ghc-register.hook ghc-unregister.hook
        0001-Replace-more-autotools-obsolete-macros-19189.patch
        0002-configure-fix-the-use-of-some-obsolete-macros-19189.patch
        0003-llvmGen-Accept-range-of-LLVM-versions.patch
        0004-Fix-autoconf-after-6d6edb1bbb.patch
        0005-Set-min-LLVM-version-to-9-and-make-version-checking-.patch
        0006-Limit-upper-version-of-Happy-for-ghc-9.2-18620.patch
        0007-Fix-dependency-on-libatomic.patch
        0008-Implement-riscv64-LLVM-backend.patch
        0009-Enable-tables-next-to-code-for-riscv64.patch)
noextract=("$_pkgbase-${pkgver}-src.tar.xz")
sha512sums=('bee7950a5118be8d8cefe0db5070139a5a93ca21c5bc6f8bf453429831f0c44f5e0fb5ee569865d6b8b92749044ee4123be06920928ac7a1ec9cffa9404a3e53'
            'd69e5222d1169c4224a2b69a13e57fdd574cb1b5932b15f4bc6c7d269a9658dd87acb1be81f52fbcf3cb64f96978b9943d10cee2c21bff0565aaa93a5d35fcae'
            '5f659651d8e562a4dcaae0f821d272d6e9c648b645b1d6ab1af61e4dd690dc5a4b9c6846753b7f935963f001bb1ae1f40cd77731b71ef5a8dbc079a360aa3f8f'
            '3bdbd05c4a2c4fce4adf6802ff99b1088bdfad63da9ebfc470af9e271c3dd796f86fba1cf319d8f4078054d85c6d9e6a01f79994559f24cc77ee1a25724af2e6'
            '6390cb76b7786efb89acc854fdf68766c149412dddbf8c5f399aa90e204e704d6e476e6d850361d66e8e821e1a69dc55cb6b3b883dcd02e9ec5c6f5288ee280b'
            'c1a4906f4a0a3abf5b2ed03a3d24c6ab097910f3fab5486c443dfc87ffbba399f95218663421ebf7e43d6d066ff0b364931d9bc3e5aa2162684f8b8511bc7531'
            'cb90cf9c3af2704acb15e9c18b1b7d6b1de32bc4014c3d631e3d34bdf07ef91b03869b0ae3721ab73acb9f7dbd2e2805462e10c9c5431ed17464dd274121228a'
            '775ef373ed60f752138d5ff728c2205b7d18ec72d8cf86152f6473a154003d0c9379d82310aeca2211123bfe48f6a5414af00d50a26680638b6db9718160199e'
            'b3aeaf4c4f9e816350b97f1060cb2de3f9dcacd2c10c714cceb5e50d47ad94e845c2dae63eea22d806437c307ab6f561bfb385ec98087465c4172e22d755f402'
            'ffc73819465dee34435b82b59f04488fd49163466814538121dc8855e3fee4ab23d9085dac108292022f3bffbc45b84cb744428ce42fe21214437b72010aae77'
            '8c1a3c7c99feffeb25c44cdeb7f40161c72e034fc2608ec92703a7bbc09e60ab4cd0899175ca20c126556762542509691d57e2a80d5430173c0a827bf51db9c7'
            '1e31ced0a68d3782d4bff9105ede96354ac8a97157625449b82006ed5bb1503b50d29114f5101461d999c31a307d8161379ceae0df0ddb33ab1b47cf3918a1ab'
            '09d8de013c3d27d77f13cbb78a4eb653121df6716cbd39b50af48e9664ce057377377c44a7243a3c2fdb2b4a48a92dd25f0723fae62597504a79d4629435ba9c')

ghc_bootstrap_root=/opt/ghc-bootstrap-riscv64

prepare() {
  # Need to extract this tarball with a UTF-8 locale instead of a chroot's "C"
  # locale; otherwise we get:
  #   bsdtar: Pathname can't be converted from UTF-8 to current locale.
  LANG=en_US.UTF-8 bsdtar xf $_pkgbase-${pkgver}-src.tar.xz

  cd ghc-$pkgver

  for f in "$srcdir/"*.patch; do
    patch -Np1 -i "$f"
  done

  cp mk/build.mk{.sample,}
  sed -i '1iBuildFlavour = perf-llvm' mk/build.mk

  # https://gitlab.haskell.org/ghc/ghc/-/issues/15689
  sed -i '/^SRC_CC_OPTS/ s/^/#/' mk/warnings.mk
}

build() {
  export PATH="$ghc_bootstrap_root/bin:$PATH"
  cd ghc-$pkgver

  ./boot && ./configure \
    --prefix=/usr \
    --docdir=/usr/share/doc/ghc \
    --with-system-libffi \
    --with-ffi-includes=$(pkg-config --variable=includedir libffi)
  LANG=en_US.UTF-8 make
}

package() {
  export PATH="$ghc_bootstrap_root/bin:$PATH"
  cd ghc-$pkgver

  make DESTDIR="$pkgdir" -j1 install

  install -Dm644 "$srcdir"/ghc-rebuild-doc-index.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-rebuild-doc-index.hook

  install -Dm644 utils/completion/ghc.bash "$pkgdir/usr/share/bash-completion/completions/ghc"

  install -Dm644 "$srcdir"/ghc-register.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-register.hook
  install -Dm644 "$srcdir"/ghc-unregister.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-unregister.hook

  install -dm755 "$pkgdir"/usr/share/haskell/{register,unregister}

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: sw=2
